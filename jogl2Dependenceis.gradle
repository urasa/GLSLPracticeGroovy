import org.apache.tools.ant.taskdefs.condition.Os

def osFamily = {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) 'windows'
    else if (Os.isFamily(Os.FAMILY_UNIX)) 'linux'
    else if (Os.isFamily(Os.MAC)) 'mac'
}
def osArch = {
    if (Os.isArch('x86')) 'i586'
    else if (Os.isArch('amd64')) 'amd64'
}
def nativeClassifier = 'natives-' + osFamily() + '-' + osArch()
def version = '2.0-rc11'

configurations.create('joglLibs')
dependencies {
    joglLibs ([['org.jogamp.jogl:jogl-all', version],
               ['org.jogamp.jogl:jogl-all', version, nativeClassifier],
               ['org.jogamp.gluegen:gluegen-rt', version],
               ['org.jogamp.gluegen:gluegen-rt', version, nativeClassifier]]
               *.join(':'))
    
    compile fileTree (dir: 'libs/jogl', include: '*.jar', exclude: '*natives*')
    runtime fileTree (dir: 'libs/jogl', include: '*natives*')
}

task syncJoglLibs(type: Sync) {
    from configurations.joglLibs
    into 'libs/jogl'
}
compileJava.dependsOn syncJoglLibs

